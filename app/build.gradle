apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def secretsFile = file(System.properties['user.home'] + '/.gradle/gradle.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "io.ffem.iitk"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 5
        versionName "Alpha 0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                storeFile file(secrets.getProperty('FFEM_RELEASE_STORE_FILE'))
                storePassword secrets.getProperty('FFEM_RELEASE_STORE_PASSWORD')
                keyAlias secrets.getProperty('FFEM_RELEASE_KEY_ALIAS')
                keyPassword secrets.getProperty('FFEM_RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + 0L + ")"
        }
        release {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Expiry date of apk. To prevent test builds from being used in production
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" +
                    (System.currentTimeMillis()) + "L)"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "iitk-crvii-${variant.versionCode}.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions { jvmTarget = "1.8" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.play:core:1.8.2'

    // Dagger
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    kapt 'com.google.dagger:dagger-android-processor:2.28.3'
    implementation 'com.google.dagger:dagger-android:2.28.3'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.28.3'

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
